plugins {
  id "com.gradle.plugin-publish" version "0.9.4"
  id "java-gradle-plugin"
  id "eclipse"
  id "jacoco"
  id "findbugs"
  id "pmd"
}

repositories {
  jcenter()
}

version = "1.1"
group = "gov.loc.repository"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  compile 'org.slf4j:slf4j-api:1.7.18',
  'org.apache.httpcomponents:httpclient:4.5.1',
  'org.apache.httpcomponents:httpmime:4.5.1',
  'com.google.code.gson:gson:2.5',
  gradleApi()

  testCompile 'junit:junit:4.12', 
  'org.mockito:mockito-core:1.10.19'
}

pluginBundle {
  website = 'https://github.com/libraryofcongress'
  vcsUrl = 'https://github.com/libraryofcongress/simple-artifact-uploader'
  description = 'Upload artifacts to artifactory using its rest api instead of the provided plugin. '
  tags = ['artifactory', 'upload', 'rest-api']

  plugins {
    artifactory {
      id = 'gov.loc.repository.simple-artifact-uploader'
      displayName = 'Simple artifact uploader'
    }
  }
}

jacoco {
  toolVersion = "0.7.5.201505241946" //needed for jenkins to report code coverage correctly
}

findbugsTest.enabled = false

findbugs {
  ignoreFailures = true //don't fail the gradle build if bugs are found
}

pmdTest.enabled = false

pmd {
  ruleSets = ["java-basic", "java-braces", "java-empty"]
}

jacocoTestReport {
  reports {
      xml.enabled = true // coveralls plugin depends on xml format report
      html.enabled = true
  }
}

jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport

//Keep this for easy viewing of html findbugs report
//tasks.withType(FindBugs) {
//  reports {
//      xml.enabled = false
//      html.enabled = true
//  }
//}
